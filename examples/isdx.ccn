changequote(`[[[', `]]]')

divert([[[-1]]])
define([[[forloop]]], [[[pushdef([[[$1]]], [[[$2]]])_forloop($@)popdef([[[$1]]])]]])
define([[[_forloop]]],
       [[[$4[[[]]]ifelse($1, [[[$3]]], [[[]]], [[[define([[[$1]]], incr($1))$0($@)]]])]]])
divert[[[]]]dnl

define([[[MAXAS]]], 10)
define([[[MASKBITS]]], 48)


typedef uint<32> IP4

typedef struct {
    [bool; MASKBITS] dstAddr,
    uint<48> srcAddr
} eth_t 

typedef struct {
    uint<8> protocol,
    IP4 src,
    IP4 dst
} ip4_t

typedef struct {
    bool valid,
    uint<16> vid
} vlan_t

typedef struct {
    eth_t    eth,
    ip4_t    ip4,
    vlan_t   vlan
} Packet

typedef uint<48> ASId
typedef struct {
    uint<32> addr,
    uint<8>  mask
} IP4Prefix

typedef uint<16> ASPort
typedef struct {
    ASId as,
    ASPort port
} ASPortDescr
typedef uint<48> SDXPort

(* 
BGP route database encoding
*)

function nAS(): ASId = 48'd[[[]]]MAXAS

(* Number of ASs connected to exchange *)
function iAS(ASId as): bool = as < nAS() 

(* Number of SDX-facing ports for each AS *)
function asNPorts(ASId as): ASPort

function iASPort(ASId as, ASPort port): bool = iAS(as) and port < asNPorts(as)

(* Database of routes advertised via BGP *)
function prefixMatch(ASId as, IP4 addr): bool

function forwardable(IP4 ip): bool = 
    false[[[]]]forloop([[[i]]], [[[0]]], decr(MAXAS), [[[ or prefixMatch(48'd[[[]]]i, ip)]]])

role ASRouterIn[ASId as, uint<8> port] | iAS(as) and port == 8'd0 = 
    havoc pkt.eth;
    if (forwardable(pkt.ip4.dst)) then
        ?send ASSDXIn[prefixMatch(.as, pkt.ip4.dst)]

role ASRouterOut[ASId as, uint<8> port] | iAS(as) and port == 8'd0 = filter false

role ASSDXIn[ASId as, ASPort port] | iASPort(as, port) = filter false

refine ASRouterIn {
    (*
    User-defined policy functions 
    *)
    function outboundPolicies(ASId as, uint<10> i): ASId
    function outboundPolicy(ASId from, ASId to, ip4_t p): bool
    assume (ASId as, uint<10> i) (i >= 10'd[[[]]]MASKBITS) => not iAS(outboundPolicies(as, i))

    function applyOutboundPolicy(ASId as, ip4_t addr): uint<10> = 
        case {
forloop([[[i]]], [[[0]]], decr(MASKBITS), [[[            outboundPolicy(as, outboundPolicies(as, 10'd[[[]]]i), addr): 10'd[[[]]]i;
]]])dnl
            default: 10'd[[[]]]MASKBITS;
        }

    function inboundPolicy(ASId as, ip4_t p): ASPort
    (* inbound policy must route packets to valid port numbers *)
    assume (ASId as, ip4_t p, ASPort port) (iAS(as) and (inboundPolicy(as, p) == port)) => iASPort(as, port)

    function nextHop(ASId as, IP4 addr): ASId

    (* nextHop is defined for all forwardable addresses *)
    assume (ASId as, IP4 ip) 
        forwardable(ip) => iAS(nextHop(as, ip))

    (* nextHop must be consistent with BGP database *)
    assume (ASId as, ASId dst, IP4 ip) 
        (iAS(dst) and (nextHop(as, ip) == dst)) => prefixMatch(dst, ip)

    role ASRouterIn[ASId as, uint<8> port] | iAS(as) and port == 8'd0 = 
        let ASId defdst = nextHop(as, pkt.ip4.dst);
        let uint<10> policy = applyOutboundPolicy(as, pkt.ip4);
        let ASId dst = outboundPolicies(as, policy);
        havoc pkt.eth;
        if iAS(dst) and prefixMatch(dst, pkt.ip4.dst) then {
            send ASSDXIn[dst, inboundPolicy(dst, pkt.ip4)]
        } else if iAS(defdst) then {
            send ASSDXIn[defdst, inboundPolicy(defdst, pkt.ip4)]
        }
}

refine ASRouterIn {

    assume (ASId as) iAS(as) => (asNPorts(as) > 16'd0)

    function iSDXPort(SDXPort port): bool

    function asSDXLink(ASId as, ASPort asport): SDXPort
    assume (ASId as, ASPort asport) iASPort(as, asport) => iSDXPort(asSDXLink(as, asport))

    function sdxASLink(SDXPort port): ASPortDescr
    (*assume iSDXPort(sdxport) => iASPort(sdxASLink(sdxport)) *)
    assume (ASId as, ASPort asport) iASPort(as, asport) => (ASPortDescr{as, asport} == sdxASLink(asSDXLink(as, asport)))

    function encode(ASId as, IP4 addr): [bool; MASKBITS] = 
        array!(prefixMatch(outboundPolicies(as, 10'd0), addr)[[[]]]forloop([[[i]]], [[[1]]], decr(MASKBITS), [[[, prefixMatch(outboundPolicies(as, 10'd[[[]]]i), addr)]]]))
    
    role ASRouterIn[ASId as, uint<8> port] | iAS(as) and port == 8'd0 = 
         pkt.eth.dstAddr := encode(as, pkt.ip4.dst)(* ++ next_hop(as, pkt.ip4.dst)*);
         pkt.eth.srcAddr := nextHop(as, pkt.ip4.dst);
         send ASSDXOut[as, 16'd0]

    role ASSDXOut[ASId as, ASPort port] | iASPort(as, port) = 
        send SDXIn[asSDXLink(as, port)]

    role SDXIn[SDXPort port] | iSDXPort(port) = 
        let ASId srcas = sdxASLink(port).as;
        let uint<10> policy = applyOutboundPolicy(srcas, pkt.ip4);
        let ASId dst = outboundPolicies(srcas, policy);
        if iAS(dst) and select!(encode(srcas, pkt.ip4.dst), policy) then {
            send SDXOut[asSDXLink(dst, inboundPolicy(dst, pkt.ip4))]
        } else {
            if iAS(pkt.eth.srcAddr) then {
                send SDXOut[asSDXLink(pkt.eth.srcAddr, inboundPolicy(pkt.eth.srcAddr, pkt.ip4))]
            }
        }

    role SDXOut[SDXPort port] | iSDXPort(port) = 
        let ASPortDescr asport = sdxASLink(port);
        send ASSDXIn[asport.as, asport.port]
}

refine {

    role AS[ASId as] | iAS(as) = filter false
    switch AS ( (ASRouterIn, ASRouterOut), (ASSDXIn, ASSDXOut))

    role SDX[] = filter false
    switch SDX ( (SDXIn, SDXOut) )
}

refine {
    function asNPorts(ASId as): ASPort = 16'd1
    function iSDXPort(SDXPort port): bool = port < 48'd[[[]]]MAXAS
    function asSDXLink(ASId as, ASPort asport): SDXPort = as
    function sdxASLink(SDXPort port): ASPortDescr = ASPortDescr{port, 16'd0}
}
