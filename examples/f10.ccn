(* Liu et al. F10: A Fault-Tolerant Engineered Network *)

typedef struct {
    uint<8> ip3,
    uint<8> ip2,
    uint<8> ip1,
    uint<8> ip0
} IP4

typedef struct {
    uint<48> dstAddr,
    uint<48> srcAddr
} eth_t

typedef struct {
    bool valid,
    uint<16> oper,
    uint<48> sha,
    uint<32> spa,
    uint<48> tha,
    uint<32> tpa
} arp_t

typedef struct {
    eth_t    eth,
    arp_t    arp
} Packet

function incmodp(uint<8> v): uint<8>
 = 
    case {
        (v + 8'd1) < p(): (v + 8'd1);
        default:          (v + 8'd1) - p();
    }

typedef struct {
    bool tree, 
    uint<8> i1,
    uint<8> i0,
    uint<8> h
} Location

(*assume (uint<8> v) (not (incmodp(v) == v)) and (incmodp(v) < p()) *)
function p(): uint<8>
assume () p() < 8'd128 and p() > 8'd2
function k(): uint<8> = p() + p()
function tree(uint<48> addr): bool    = addr[0:0] == 1'd1
function i1(uint<48> addr): uint<8>   = addr[15:8]
function i0(uint<48> addr): uint<8>   = addr[23:16]
function port(uint<48> addr): uint<8> = addr[31:24]

function iL0Port(uint<8> i1, uint<8> i0, uint<8> port): bool = i1 < p() and i0 < p() and port < p()
function goodPacket(Packet p): bool = 
    iL0Port(i1(p.eth.dstAddr), i0(p.eth.dstAddr), port(p.eth.dstAddr))
function macEncode(Location loc): uint<48> = 
    16'h0200 ++ loc.h ++ loc.i0 ++ loc.i1 ++ case {loc.tree: 8'd1; default: 8'd2;}
function arpResponse(arp_t arp): arp_t = 
    arp_t { true
          , 16'd2 (* ARP response *)
          , macEncode(ip2mac(IP4{arp.tpa[31:24], arp.tpa[23:16], arp.tpa[15:8], arp.tpa[7:0]}))
          , arp.tpa
          , arp.sha
          , arp.spa
          }

role L0In[bool tree, uint<8> i1, uint<8> i0, uint<8> port] | iL0Port(i1,i0,port) = 
    if pkt.arp.valid and (pkt.arp.oper == 16'd1 (*ARP Request*)) then {
        pkt.arp := arpResponse(pkt.arp);
        pkt.eth.srcAddr := pkt.arp.sha;
        pkt.eth.dstAddr := pkt.arp.tha;
        send L0Out[tree, i1, i0, port]
    } else if goodPacket(pkt) (* Validate packets at the edge *) then {
        pkt.eth.srcAddr := macEncode(Location{tree, i1, i0, port});
        (*pkt.eth.dstAddr := 48'hffffffffffff;*)
        send L0Out[tree(pkt.eth.dstAddr), i1(pkt.eth.dstAddr), i0(pkt.eth.dstAddr), port(pkt.eth.dstAddr)]
    }

role L0Out[bool tree, uint<8> i1, uint<8> i0, uint<8> port] | iL0Port(i1,i0,port) = 
     send HostIn[macEncode(Location{tree, i1, i0, port}), mac2ip(tree, i1, i0, port), 8'd0]

function ip2mac(IP4 addr) : Location = Location {addr.ip2[0:0] == 1'd1, addr.ip2 >> 8'd1 , addr.ip1, addr.ip0 - 8'd1}
function mac2ip(bool tree, uint<8> i1, uint<8> i0, uint<8> port) : IP4 = 
    IP4 { 8'd10
        , (i1 << 8'd1) + (case {tree: 8'd1; default: 8'd0;})
        , i0
        , port + 8'd1 
        }

function iHost(uint<48> mac, IP4 ip): bool = 
    (i1(mac) < p()) and (i0(mac) < p()) and (port(mac) < p()) and (mac[47:32] == 16'h0200) and (mac[7:0] == 8'd1 or mac[7:0] == 8'd2)
    and
    (ip == mac2ip(tree(mac), i1(mac), i0(mac), port(mac)))

role HostIn[uint<48> mac, IP4 ip, uint<8> port] | iHost(mac, ip) and port == 8'd0 = (filter false)
role HostOut[uint<48> mac, IP4 ip, uint<8> port] | iHost(mac, ip) and port == 8'd0 =
    send L0In[tree(mac), i1(mac), i0(mac), port(mac)]


refine L0In {

    function hash(eth_t eth): uint<8>
    assume (eth_t e) hash(e) < p()

    function l1SwitchUp(bool tree, uint<8> i1, uint<8> r): bool
    assume (bool tree, uint<8> i1, uint<8> r1, uint<8> r2) 
        ((not (r1 == r2)) and (not l1SwitchUp(tree, i1, r1))) => l1SwitchUp(tree, i1, r2)

    function l1Avoid(bool tree, uint<8> i1, uint<8> r): bool
    assume (bool tree, uint<8> i1, uint<8> r)
        l1Avoid(tree, i1, r) => (not l1SwitchUp(tree, i1, r))

    assume (bool tree, uint<8> i1_1, uint<8> i1_2, uint<8> r) 
        (not (i1_1 == i1_2) and (not l1SwitchUp(tree, i1_1, r))) => l1SwitchUp(tree, i1_2, r)

    function pickdef01port(bool tree, Packet p): uint<8> =
        case {
            tree(p.eth.dstAddr) == tree and l1Avoid(tree, i1(p.eth.dstAddr), hash(p.eth)): incmodp(hash(p.eth));
            default: hash(p.eth);
        }

    function pick01port(bool tree, uint<8> i1, uint<8> defport): uint<8> =
        case {
            l1SwitchUp(tree, i1, defport): defport;
            default: incmodp(defport);
        }

    role L0In[bool tree, uint<8> i1, uint<8> i0, uint<8> port] | iL0Port(i1,i0,port) = 
        if pkt.arp.valid and (pkt.arp.oper == 16'd1 (*ARP Request*)) then {
            pkt.arp := arpResponse(pkt.arp);
            pkt.eth.srcAddr := pkt.arp.sha;
            pkt.eth.dstAddr := pkt.arp.tha;
            send L0Out[tree, i1, i0, port]
        } else if goodPacket(pkt) then {
            pkt.eth.srcAddr := macEncode(Location{tree, i1, i0, port});
            if tree(pkt.eth.dstAddr) == tree and i1(pkt.eth.dstAddr) == i1 and i0(pkt.eth.dstAddr) == i0 then {
                (*pkt.eth.dstAddr := 48'hffffffffffff;*)
                send L0Out[tree, i1, i0, port(pkt.eth.dstAddr)]
            } else {
                send L01Out[tree, i1, i0, pick01port(tree, i1, pickdef01port(tree, pkt))]
            }
        }

    role L01Out[bool tree, uint<8> i1, uint<8> i0, uint<8> port] | iL0Port(i1,i0,port) / goodPacket(pkt) = 
        if l1SwitchUp(tree, i1, port) then
            send L10In[tree, i1, port, i0]

    role L01In[bool tree, uint<8> i1, uint<8> i0, uint<8> port] | iL0Port(i1,i0,port) / goodPacket(pkt) = 
        (*pkt.eth.dstAddr := 48'hffffffffffff;*)
        send L0Out[tree, i1, i0, port(pkt.eth.dstAddr)]

    function iL1Port(uint<8> i1, uint<8> r, uint<8> port): bool = 
        i1 < p() and r < p() and port < p()

    role L10In[bool tree, uint<8> i1, uint<8> r, uint<8> port] | iL1Port(i1,r,port) / goodPacket(pkt) = 
        ?send L10Out[.tree==tree(pkt.eth.dstAddr) and .i1 == i1(pkt.eth.dstAddr) and .r < p() and .port == i0(pkt.eth.dstAddr)]

    role L10Out[bool tree, uint<8> i1, uint<8> r, uint<8> port] | iL1Port(i1,r,port) / goodPacket(pkt) = 
        send L01In[tree, i1, port, r]
}

refine L10In {

    function l2SwitchUp(uint<8> x, uint<8> y): bool
    assume (uint<8> x1, uint<8> y1, uint<8> x2, uint<8> y2) 
        ((not l2SwitchUp(x1, y1)) and (not (x1 == x2 and y1 == y2))) => l2SwitchUp(x2, y2)

    function pickdef12port(bool tree, Packet p): uint<8> =
        case {
            tree(p.eth.dstAddr) != tree and l1Avoid(tree(p.eth.dstAddr), i1(p.eth.dstAddr), hash(p.eth)): incmodp(hash(p.eth));
            default: hash(p.eth);
        }

    function pick12port(bool tree, uint<8> i1, uint<8> r, uint<8> defport): uint<8> =
        case {
            tree == false: 
                case {
                    l2SwitchUp(r, defport): defport;
                    default: incmodp(defport);};
            default: 
                case {
                    l2SwitchUp(defport, r): defport;
                    default: incmodp(defport);};
        }

    role L10In[bool tree, uint<8> i1, uint<8> r, uint<8> port] | iL1Port(i1,r,port) / goodPacket(pkt) = 
        if tree(pkt.eth.dstAddr) == tree and i1(pkt.eth.dstAddr) == i1 then {
            if i0(pkt.eth.dstAddr) < p() then {
                send L10Out[tree, i1, r, i0(pkt.eth.dstAddr)]
            }
        } else {
            send L12Out[tree, i1, r, pick12port(tree, i1, r, pickdef12port(tree, pkt))]
        }
  
    role L12Out[bool tree, uint<8> i1, uint<8> r, uint<8> port] | iL1Port(i1,r,port) / goodPacket(pkt) = 
        if tree == false then {
             if l2SwitchUp(r, port) then
                send L2In[r, port, i1]           
        } else {
            if l2SwitchUp(port, r) then
                send L2In[port, r, p() + i1]
        }

    function iL2Port(uint<8> x, uint<8> y, uint<8> port): bool = 
        x < p() and y < p() and port < k()

    role L2In[uint<8> x, uint<8> y, uint<8> port] | iL2Port(x,y,port) / goodPacket(pkt) = 
        if tree(pkt.eth.dstAddr) == false then {
             if l1SwitchUp(false, i1(pkt.eth.dstAddr), x) then {
                send L2Out[x, y, i1(pkt.eth.dstAddr)]
             } else if l1SwitchUp(true, hash(pkt.eth), y) then {
                send L2Out[x, y, p() + hash(pkt.eth)]
             } else {
                send L2Out[x, y, p() + incmodp(hash(pkt.eth))]
             }
        } else {
             if l1SwitchUp(true, i1(pkt.eth.dstAddr), y) then {
                send L2Out[x, y, p() + i1(pkt.eth.dstAddr)]
             } else if l1SwitchUp(false, hash(pkt.eth), x) then {
                send L2Out[x, y, hash(pkt.eth)]
             } else {
                send L2Out[x, y, incmodp(hash(pkt.eth))]
             }
        }

    role L2Out[uint<8> x, uint<8> y, uint<8> port] | iL2Port(x,y,port) / goodPacket(pkt) = 
        if (port < p()) then {
             if l1SwitchUp(false, port, x) then
                send L12In[false, port, x, y]
        } else {
             if l1SwitchUp(true, port - p(), y) then
                send L12In[true, port - p(), y, x]
        }

    role L12In[bool tree, uint<8> i1, uint<8> r, uint<8> port] | iL1Port(i1,r,port) / goodPacket(pkt) = 
        if (tree == tree(pkt.eth.dstAddr) and i1 == i1(pkt.eth.dstAddr)) then {
            send L10Out[tree, i1, r, i0(pkt.eth.dstAddr)]
        } else {
            send L12Out[tree, i1, r, pick12port(tree, i1, r, incmodp(port))]
        }
}

refine {
    role L2Switch[uint<8> x, uint<8> y] | x < p() and y < p() = filter false
    switch L2Switch( (L2In, L2Out) )

    role L1Switch[bool tree, uint<8> i1, uint<8> r] | i1 < p() and r < p() = filter false
    switch L1Switch( (L10In, L10Out), (L12In, L12Out) )

    role L0Switch[bool tree, uint<8> i1, uint<8> i0] | i1 < p() and i0 < p() = filter false
    switch L0Switch( (L0In, L0Out), (L01In, L01Out) )


    role Host[uint<48> mac, IP4 ip] | iHost(mac, ip) = (filter false)
    host Host((HostIn, HostOut))
}

refine {
    function p(): uint<8> = 8'd3
}
