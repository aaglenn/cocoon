define(`VIRT_STYLE', `VLAN')

include(`virtualization.m4.ccn')

(* VLAN-based virtualization on top of P2P tunnels *)

refine VHostVOut {

    (* NetKAT cannot do ARP, so we do not support L3 vnets *)
    function iL3VNet(VNetId id): bool = false
    assume (VNetId vid) iL2VNet(vid) => (not (vid == 16'd0))
    
    (* Physical topology *)
    function vHostLocation(VHostId vhost): HostId
    assume (VHostId vhost) iVHost(vhost) => iHost(vHostLocation(vhost))
    
    function iVSwitchPort(HostId hst, uint<16> swport): bool
    assume (HostId hst, uint<16> swport) iVSwitchPort(hst, swport) => iHost(hst)

    function hHostsVNet(HostId hst, VNetId vnet): bool
    assume (VHPortId vhport, VNetId vnet, HostId hst) 
           (vHPortVNet(vhport) == vnet and vHostLocation(vhport.vhost) == hst) => hHostsVNet(hst, vnet) 


    function vH2SwLink(VHPortId hport): uint<16>
    function vSw2HLink(HostId hst, uint<16> swport): VHPortId

    function vSwPortVNet(HostId hst, uint<16> swport): VNetId =
        vHPortVNet(vSw2HLink(hst, swport))

    function downTunVnet(): VNetId
    function upTunVnet(): VNetId
    assume (VNetId vnet) iVNet(vnet) => (downTunVnet() != vnet)
    assume (VNetId vnet) iVNet(vnet) => (upTunVnet() != vnet)
    assume () upTunVnet() != downTunVnet()

    (* link symmetry: break it into 5 assertions to help z3 handle quantifiers *)
    (*assume (VHPortId hport, IP4 ip, uint<16> swport) 
           (iVHostPort(hport) and vHostLocation(hport.vhost) == ip and vH2SwLink(hport) == swport) == ((iVSwitchPort(ip, swport)) and (vSw2HLink(ip, swport) == hport)) *)
    
    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVHostPort(hport) and vHostLocation(hport.vhost) == hst and vH2SwLink(hport) == swport) => (iVSwitchPort(hst, swport))
    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVHostPort(hport) and vHostLocation(hport.vhost) == hst and vH2SwLink(hport) == swport) => (vSw2HLink(hst, swport) == hport)
    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVSwitchPort(hst, swport) and vSw2HLink(hst, swport) == hport) => iVHostPort(hport)
    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVSwitchPort(hst, swport) and vSw2HLink(hst, swport) == hport) => (vHostLocation(hport.vhost) == hst)
    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVSwitchPort(hst, swport) and vSw2HLink(hst, swport) == hport) => (vH2SwLink(hport) == swport)

    role VHostVOut[VHostId vhost, uint<8> port] | iVHostPort(VHPortId{vhost, port}) =
        send VSwitchIn[vHostLocation(vhost), vH2SwLink(VHPortId{vhost, port})]
   
    role VSwitchIn[HostId hst, uint<16> vport] | iVSwitchPort(hst, vport) = 
        let VHPortId from_vhport = vSw2HLink(hst, vport);
        if pkt.eth.srcAddr == vHPort2Mac(from_vhport) and pkt.vlan.vid == 16'd0 then {
            (* Destination exists? *)
            let VNetId vnet = vSwPortVNet(hst, vport);
            if bCastMAC() == pkt.eth.dstAddr then {
                fork(HostId dhst | iHost(dhst) and hHostsVNet(dhst, vnet)) {
                    if dhst == hst then {
                        fork(VHPortId vhport | iVHostPort(vhport) and 
                                               vHPortVNet(vhport) == vnet and 
                                               vHostLocation(vhport.vhost) == hst and 
                                               vH2SwLink(vhport) != vport and
                                               connection(from_vhport, vhport)) {
                            send VSwitchOut[hst, vH2SwLink(vhport)]
                        }
                    } else {
                        pkt.vlan.vid := vnet;
                        send VSwitchTunOut[hst, dhst]
                    }
                }
            } else {
                let VHPortId to_vhport = mac2VHPort(vnet, pkt.eth.dstAddr);
                if iVHostPort(to_vhport) and connection(from_vhport, to_vhport) then {
                    if (vHostLocation(to_vhport.vhost) == hst) then { 
                        (* Local destination -- deliver *)
                        send VSwitchOut[hst, vH2SwLink(to_vhport)]
                    } else {
                        (* Remote destination -- encapsulate *)
                        pkt.vlan.vid := vnet;
                        send VSwitchTunOut[hst, vHostLocation(to_vhport.vhost)]

                    }
                }
            }
        }
        
    role VSwitchOut[HostId hst, uint<16> vport] | iVSwitchPort(hst, vport) = 
        let VHPortId vhport = vSw2HLink(hst, vport);
        send VHostVIn[vhport.vhost, vhport.vport]

    function iTunPort(HostId hst, HostId rhst): bool = iHost(hst) and iHost(rhst) and not (rhst == hst)
    
    role VSwitchTunOut[HostId hst, HostId rhst] | iTunPort(hst, rhst)/ pkt.vlan.vid != upTunVnet() and pkt.vlan.vid != downTunVnet() = 
        send VSwitchTunIn[rhst, hst]

    role VSwitchTunIn[HostId hst, HostId rhst] | iTunPort(hst, rhst) = 
        fork (VNetId vnet | iL2VNet(vnet) and hHostsVNet(hst, vnet)) {
            if (pkt.vlan.vid == vnet) then {
                pkt.vlan.vid := 16'd0;
                if pkt.eth.dstAddr == bCastMAC() then {
                    fork(VHPortId vhport | iVHostPort(vhport) and 
                                           vHPortVNet(vhport) == vnet and 
                                           vHostLocation(vhport.vhost) == hst and 
                                           connection(mac2VHPort(vnet, pkt.eth.srcAddr), vhport)) {
                        send VSwitchOut[hst, vH2SwLink(vhport)]
                    }
                } else {
                    send VSwitchOut[hst, vH2SwLink(mac2VHPort(vnet, pkt.eth.dstAddr))]
                }
            }
        }
}

(*
refine VHostPOut, VSwitchPlainIn {

    function iVSwitchPPort(HostId hst, uint<16> swport): bool
    assume (HostId hst, uint<16> swport) iVSwitchPPort(hst, swport) => iHost(hst)

    function vH2SwPLink(VHPortId hport): uint<16>
    function vSw2HPLink(HostId hst, uint<16> swport): VHPortId

    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVHostPPort(hport) and vHostLocation(hport.vhost) == hst and vH2SwPLink(hport) == swport) => (iVSwitchPPort(hst, swport))
    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVHostPPort(hport) and vHostLocation(hport.vhost) == hst and vH2SwPLink(hport) == swport) => (vSw2HPLink(hst, swport) == hport)
    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVSwitchPPort(hst, swport) and vSw2HPLink(hst, swport) == hport) => iVHostPPort(hport)
    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVSwitchPPort(hst, swport) and vSw2HPLink(hst, swport) == hport) => (vHostLocation(hport.vhost) == hst)
    assume (VHPortId hport, HostId hst, uint<16> swport) 
           (iVSwitchPPort(hst, swport) and vSw2HPLink(hst, swport) == hport) => (vH2SwPLink(hport) == swport)

    role VHostPOut[VHostId vhost, uint<8> port] | iVHostPPort(VHPortId{vhost, port}) = 
        send VSwitchPIn[vHostLocation(vhost), vH2SwPLink(VHPortId{vhost, port})]

    role VSwitchPIn[HostId hst, uint<16> vport] | iVSwitchPPort(hst, vport) =
        let HostId dsthost = v2pPort(vSw2HPLink(hst, vport));
        if pkt.vxlan == novxlan() then {
            if (dsthost == hst) then {
                send VSwitchPlainOut[hst]
            } else {
                pkt.vlan.vid := downTunVnet();
                pkt := vxlanEncapsulate( pkt
                                       , downTunVnet()
                                       , hostIP(hst)
                                       , hostIP(dsthost));
                send VSwitchTunOut[hst]
            }
        }

    role VSwitchPOut[HostId hst, uint<16> vport] | iVSwitchPPort(hst, vport) =
        let VHPortId vhport = vSw2HPLink(hst, vport);
        send VHostPIn[vhport.vhost, vhport.vport]

    role VSwitchTunOut[HostId hst] | iHost(hst) / (pkt.vxlan.vni == upTunVnet() or pkt.vxlan.vni == downTunVnet()) = 
    
        let HostId dhst = ip2Host(pkt.ip4.dst);
        if (pkt.ip4.valid and iHost(dhst)) then
            send VSwitchTunIn[dhst]

    role VSwitchTunIn[HostId hst] | iHost(hst) / pkt.vxlan.valid = 
        if pkt.vxlan.vni == downTunVnet() then {
            pkt := vxlanDecapsulate(pkt);
            send VSwitchPlainOut[hst]
        } else {
            let VHPortId vport = p2vPort(ip2Host(pkt.ip4.src));
            if iVHostPPort(vport) then {
                pkt := vxlanDecapsulate(pkt);
                send VSwitchPOut[hst, vH2SwPLink(vport)]           
            }
        }

    role VSwitchPlainIn[HostId hst] | iHost(hst) / pkt.vxlan == novxlan() = 
        let VHPortId vport = p2vPort(hst);
        if iVHostPPort(vport) then {
            let HostId dsthost = vHostLocation(vport.vhost);
            if dsthost == hst then {
                send VSwitchPOut[hst, vH2SwPLink(vport)]
            } else {
                pkt := vxlanEncapsulate( pkt
                                       , upTunVnet()
                                       , hostIP(hst)
                                       , hostIP(dsthost));
                send VSwitchTunOut[hst]
            }
        }
}

refine VSwitchTunOut, VSwitchVXLANOut, VSwitchPlainOut{

    role VSwitchTunOut[HostId hst] | iHost(hst) / (pkt.vxlan.vni == upTunVnet() or pkt.vxlan.vni == downTunVnet()) = 
        send HostOut[hst]

    role VSwitchVXLANOut[HostId hst] | iHost(hst) / pkt.vxlan.valid and pkt.vxlan.vni != upTunVnet() and pkt.vxlan.vni != downTunVnet() = 
        send HostOut[hst]

    role VSwitchPlainOut[HostId hst] | iHost(hst) / not pkt.vxlan.valid = 
        send HostOut[hst]

    role HostOut[HostId hst] | iHost(hst) = 
        let HostId dhst = ip2Host(pkt.ip4.dst);
        if (pkt.ip4.valid and iHost(dhst)) then
            send HostIn[dhst]

    role HostIn[HostId hst] | iHost(hst) = 
        if pkt.vxlan.valid then {
           if pkt.vxlan.vni == upTunVnet() or pkt.vxlan.vni == downTunVnet() then {
                send VSwitchTunIn[hst]
           } else {
                send VSwitchVXLANIn[hst]
           }
        } else {
           send VSwitchPlainIn[hst]
        }
}
*)

refine {
    function iL2VNet(VNetId id): bool = id == 16'd1 or id == 16'd2
    function iVHost(VHostId id): bool = 
        id == 32'd0 or id == 32'd1 or id == 32'd2 or id == 32'd3 or id == 32'd4 or id == 32'd5
    function iVHostPort(VHPortId port): bool = 
        iVHost(port.vhost) and port.vport == 8'd0
    function vHPortVNet(VHPortId port): VNetId = 
        case {
            port.vhost == 32'd0 or port.vhost == 32'd1 or port.vhost == 32'd2 : 16'd1;
            port.vhost == 32'd3 or port.vhost == 32'd4 or port.vhost == 32'd5 : 16'd2;
            default: 16'hffff;
        }
    function vHPort2Mac(VHPortId port): MAC = case {
            port.vhost == 32'd0 and port.vport == 8'd0: 48'h020000000001;
            port.vhost == 32'd1 and port.vport == 8'd0: 48'h020000000002;
            port.vhost == 32'd2 and port.vport == 8'd0: 48'h020000000003;
            port.vhost == 32'd3 and port.vport == 8'd0: 48'h020000000004;
            port.vhost == 32'd4 and port.vport == 8'd0: 48'h020000000005;
            port.vhost == 32'd5 and port.vport == 8'd0: 48'h020000000006;
            default: 48'h0;
        }
    function mac2VHPort(VNetId vnet, MAC mac): VHPortId = case {
            vnet == 16'd1 and mac == 48'h020000000001: VHPortId{32'd0, 8'd0};
            vnet == 16'd1 and mac == 48'h020000000002: VHPortId{32'd1, 8'd0};
            vnet == 16'd1 and mac == 48'h020000000003: VHPortId{32'd2, 8'd0};
            vnet == 16'd2 and mac == 48'h020000000004: VHPortId{32'd3, 8'd0};
            vnet == 16'd2 and mac == 48'h020000000005: VHPortId{32'd4, 8'd0};
            vnet == 16'd2 and mac == 48'h020000000006: VHPortId{32'd5, 8'd0};
            default: VHPortId{32'hffffffff, 8'hff};
        }

    function iHost(HostId hst): bool = 
        hst == 48'd0 or hst == 48'd1

    function hostIP(HostId hst): IP4 = 
        case {
            hst == 48'd0: 32'h10000001;
            hst == 48'd1: 32'h10000002;
            default: 32'h0;
        }

    function iVSwitchPort(HostId hst, uint<16> swport): bool = 
        (hst == 48'd0 and (swport == 16'd0 or swport == 16'd1 or swport == 16'd2)) or
        (hst == 48'd1 and (swport == 16'd0 or swport == 16'd1 or swport == 16'd2))

    function vHostLocation(VHostId vhost): HostId = 
        case {
            vhost == 32'd0 or vhost == 32'd2 or vhost == 32'd4: 48'd0;
            default: 48'd1;
        }
    function vH2SwLink(VHPortId hport): uint<16> = 
        case {
            hport == VHPortId{32'd0, 8'd0}: 16'd0;
            hport == VHPortId{32'd1, 8'd0}: 16'd0;
            hport == VHPortId{32'd2, 8'd0}: 16'd1;
            hport == VHPortId{32'd3, 8'd0}: 16'd1;
            hport == VHPortId{32'd4, 8'd0}: 16'd2;
            hport == VHPortId{32'd5, 8'd0}: 16'd2;
            default: 16'd0;
        }
    function vSw2HLink(HostId hst, uint<16> swport): VHPortId = 
        case {
            hst == 48'd0 and swport == 16'd0: VHPortId{32'd0, 8'd0};
            hst == 48'd0 and swport == 16'd1: VHPortId{32'd2, 8'd0};
            hst == 48'd0 and swport == 16'd2: VHPortId{32'd4, 8'd0};
            hst == 48'd1 and swport == 16'd0: VHPortId{32'd1, 8'd0};
            hst == 48'd1 and swport == 16'd1: VHPortId{32'd3, 8'd0};
            hst == 48'd1 and swport == 16'd2: VHPortId{32'd5, 8'd0};
            default: VHPortId{32'd0, 8'd0};
        }

    function iVHostPPort(VHPortId port): bool = false
    function iVSwitchPPort(HostId hst, uint<16> swport):bool = false 

(*
    role VHost[VHostId vhost] | iVHost(vhost) = filter false
    host VHost( (VHostVIn, VHostVOut) )

    role VSwitch[IP4 hst] | iHost(hst) = filter false
    switch VSwitch((VSwitchIn, VSwitchOut), (VSwitchVXLANIn, VSwitchVXLANOut)) *)
}
