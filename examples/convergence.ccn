(* Secure home network *)

typedef struct {
    uint<8> ip3,
    uint<8> ip2,
    uint<8> ip1,
    uint<8> ip0
} IP4

typedef struct {
    uint<48> dstAddr,
    uint<48> srcAddr
} eth_t

typedef struct {
    bool valid,
    uint<16> oper,
    uint<48> sha,
    uint<32> spa,
    uint<48> tha,
    uint<32> tpa
} arp_t

typedef struct {
    bool valid,
    uint<8> protocol,
    IP4 src,
    IP4 dst
} ip4_t

typedef struct {
    bool     valid,
    uint<24> vni,
    eth_t    eth,
    ip4_t    ip4
} vxlan_t

typedef struct {
    eth_t    eth,
    ip4_t    ip4,
    vxlan_t  vxlan
} Packet

typedef uint<24> VNetId
typedef uint<32> VHostId
typedef struct {
    VHostId vhost,
    uint<8> vport
} VHPortId

(*  TODO: 
    - basic virtualization
    - Broad/multicast
    - Secure router + verified security policy
    - Multiple physical networks
 *)

(* Virtual network topology *)
function iVNet(VNetId id): bool
function iVHost(VHostId id): bool
function iVHostPort(VHPortId port): bool
assume (VHPortId port) iVHostPort(port) => iVHost(port.vhost)

function vHPortVNet(VHPortId port): VNetId

function vHPort2Mac(VHPortId port): uint<48>
function mac2VHPort(VNetId vnet, uint<48> mac): VHPortId
assume (VHPortId port, uint<48> mac, VNetId vnet) (* bijection *)
       iVHostPort(port) => 
           ((vHPortVNet(port) == vnet and vHPort2Mac(port) == mac) <=> (mac2VHPort(vnet, mac) == port))

role VHostIn(VHPortId port) | iVHostPort(port) = filter false

role VHostOut(VHPortId port) | iVHostPort(port) = 
    (* Deliver to destination MAC within virtual network *)
    let VHPortId vhport := mac2VHPort(vHPortVNet(port), pkt.eth.dstAddr);
    if iVHostPort(vhport) then
       send VHostIn(vhport)

refine VHostOut {

    (* Physical topology *)

    function iHost(IP4 addr): bool
    function iVSwitchPort(IP4 hst, uint<16> swport): bool
    assume (IP4 hst, uint<16> swport) iVSwitchPort(hst, swport) => iHost(hst)

    function vHostLocation(VHostId vhost): IP4

    function vH2SwLink(VHPortId hport): uint<16>
    function vSw2HLink(IP4 addr, uint<16> swport): VHPortId

    function vSwPortVNet(IP4 addr, uint<16> swport): VNetId =
        vHPortVNet(vSw2HLink(addr, swport))

    assume (VHPortId hport, IP4 ip, uint<16> swport) (* link symmetry *)
           (iVHostPort(hport) and vHostLocation(hport.vhost) == ip and vH2SwLink(hport) == swport) <=> (iVSwitchPort(ip, swport) and vSw2HLink(ip, swport) == hport)

    role VHostOut(VHPortId port) | iVHostPort(port) =
        send VSwitchIn(vHostLocation(port.vhost), vH2SwLink(port))

    function vxlanEncapsulate(Packet p, VNetId vnet, IP4 src, IP4 dst): Packet =
        Packet{ eth_t{48'd0, 48'd0}
              , ip4_t{true, 8'd17(*UDP*), src, dst}
              , vxlan_t{true, vnet, p.eth, p.ip4}}

    function vxlanDecapsulate(Packet p, VNetId vnet, IP4 src, IP4 dst): Packet =
        Packet{ p.vxlan.eth
              , p.vxlan.ip4
              , vxlan_t{false, 24'd0, eth_t{48'd0, 48'd0}, ip4_t{false, 8'd0, IP4{8'd0, 8'd0, 8'd0, 8'd0}, IP4{8'd0, 8'd0, 8'd0, 8'd0}}}}

    role VSwitchIn(IP4 hst, uint<16> vport) | iVSwitchPort(hst, vport) = 
        (* Destination exists? *)
        let VNetId vnet := vSwPortVNet(hst, vport);
        let VHPortId vhport := mac2VHPort(vnet, pkt.eth.dstAddr);
        if iVHostPort(vhport) then {
            if (vHostLocation(vhport.vhost) == hst) then { 
                (* Local destination -- deliver *)
                send VSwitchOut(hst, vH2SwLink(vhport.vport))
            } else {
                (* Remote destination -- encapsulate *)
                pkt := vxlanEncap( pkt
                                 , vnet
                                 , hst
                                 , vHostLocation(vhport.vhost));
                send HostOut(hst, 8'd0)
            }
        }
        
    role VSwitchOut(IP4 hst, uint<16> vport) | iVHostPort(hst, vport) = 
        send VHostIn(vSw2HLink(hst, vport))

    role HostOut(IP4 hst, uint<8> port) | iHost(hst) and port == 8'd0 = 
        if (pkt.ip4.valid && iHost(pkt.ip4.dst)) then
            send HostIn(pkt.ip4.dst, 8'd0)

    role HostIn(IP4 hst, uint<8> port) | iHost(hst) and port == 8'd0 = 
        if pkt.vxlan.valid then {
            (* Decapsulate *)
            let vxlan_t vxlan := pkt.vxlan;
            pkt := vxlanDecapsulate(pkt);
            send VSwitchOut(hst, vH2SwLink(mac2VHPort(vxlan.vni, vxlan.eth.dstAddr)))
        }
}
